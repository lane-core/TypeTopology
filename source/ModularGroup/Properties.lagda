Lane Biocini, 17 October 2023

Elementary proofs about ğ“œ

\begin{code}

{-# OPTIONS --without-K --exact-split --safe #-}

module ModularGroup.Properties where

open import MLTT.Spartan
open import MLTT.Plus-Properties using (+functor)
open import MLTT.Unit-Properties using (ğŸ™-is-not-ğŸ˜)

open import UF.DiscreteAndSeparated using (is-discrete; discrete-types-are-sets)
open import UF.Sets using (is-set)

open import ModularGroup.Type

s-quotiented : id âˆ¼ s âˆ˜ s
s-quotiented E = refl
s-quotiented S = refl
s-quotiented (ğ’” x) = refl
s-quotiented (Î¸ x) = refl

r-quotiented : id âˆ¼ r âˆ˜ r âˆ˜ r
r-quotiented (Î· x) = refl
r-quotiented (ğ’“ x) = refl
r-quotiented (ğ’“Â² x) = refl

s-left-cancellable : left-cancellable s
s-left-cancellable {x} {y} p = s-quotiented x âˆ™ ap s p âˆ™ s-quotiented y â»Â¹

r-left-cancellable : left-cancellable r
r-left-cancellable {x} {y} p = r-quotiented x âˆ™ ap rÂ² p âˆ™ r-quotiented y â»Â¹

rÂ²-left-cancellable : left-cancellable rÂ²
rÂ²-left-cancellable = r-left-cancellable âˆ˜ r-left-cancellable

if-s-equals-id : (x : ğ“œ) â†’ s x ï¼ E â†’ x ï¼ S
if-s-equals-id x = s-left-cancellable

if-r-equals-id : (x : ğ“œ) â†’ r x ï¼ E â†’ x ï¼ RÂ²
if-r-equals-id x = r-left-cancellable

if-rÂ²-equals-id : (x : ğ“œ) â†’ rÂ² x ï¼ E â†’ x ï¼ R
if-rÂ²-equals-id x = rÂ²-left-cancellable

Î·-left-cancellable : left-cancellable Î·
Î¸-left-cancellable : left-cancellable Î¸
Î·-left-cancellable {ğ¸} {ğ¸} p = refl
Î·-left-cancellable {ğ‘†} {ğ‘†} p = refl
Î·-left-cancellable {ğ‘  x} {ğ‘  y} p = ap ğ‘  (Î¸-left-cancellable (ap tail p))
Î¸-left-cancellable {ğ‘Ÿ x} {ğ‘Ÿ y} p = ap ğ‘Ÿ (Î·-left-cancellable (ap tail p))
Î¸-left-cancellable {ğ‘ŸÂ² x} {ğ‘ŸÂ² y} p = ap ğ‘ŸÂ² (Î·-left-cancellable (ap tail (ap tail p)))

\end{code}

Negation proofs

\begin{code}

S-is-not-E : S â‰  E
S-is-not-E p = ğŸ™-is-not-ğŸ˜ (g (Î·-left-cancellable p))
 where
  f : (x : ğ“¢) â†’ ğ“¤â‚€ Ì‡
  f ğ¸ = ğŸ˜
  f ğ‘† = ğŸ™
  f (ğ‘  x) = ğŸ˜

  g : ğ‘† ï¼ ğ¸ â†’ ğŸ™ ï¼ ğŸ˜
  g = ap f

Î·-is-not-Î¸ : (x : ğ“¢) (y : ğ“¡) â†’ Î· x â‰  Î¸ y
Î·-is-not-Î¸ x y p = ğŸ™-is-not-ğŸ˜ (g p)
 where
  f : ğ“œ â†’ ğ“¤â‚€ Ì‡
  f (Î· x) = ğŸ™
  f (Î¸ x) = ğŸ˜

  g : Î· x ï¼ Î¸ y â†’ ğŸ™ ï¼ ğŸ˜
  g = ap f

Î¸-is-not-Î· : (x : ğ“¡) (y : ğ“¢) â†’ Î¸ x â‰  Î· y
Î¸-is-not-Î· x y p = Î·-is-not-Î¸ y x (p â»Â¹)

\end{code}

Proofs about the decidability of ğ“œ

\begin{code}

fibers-of-Î·-over-E : (x : ğ“¢) â†’ is-decidable (Î· x ï¼ E)
fibers-of-Î·-over-E ğ¸ = inl refl
fibers-of-Î·-over-E ğ‘† = inr S-is-not-E
fibers-of-Î·-over-E (ğ‘  x) = inr (Î» p â†’ Î¸-is-not-Î· x ğ‘† (ap s p))

fibers-of-Î·-over-S : (x : ğ“¢) â†’ is-decidable (Î· x ï¼ S)
fibers-of-Î·-over-S ğ¸ = inr (Î» p â†’ S-is-not-E (p â»Â¹))
fibers-of-Î·-over-S ğ‘† = inl refl
fibers-of-Î·-over-S (ğ‘  x) = inr Î» p â†’ Î¸-is-not-Î· x ğ¸ (ap s p)

fibers-of-Î·-over-ğ‘  : (x : ğ“¢) (y : ğ“¡) â†’ is-decidable (Î· x ï¼ Î· (ğ‘  y))
fibers-of-Î¸-over-ğ‘Ÿ : (x : ğ“¡) (y : ğ“¢) â†’ is-decidable (Î¸ x ï¼ Î¸ (ğ‘Ÿ y))
fibers-of-Î¸-over-ğ‘ŸÂ² : (x : ğ“¡) (y : ğ“¢) â†’ is-decidable (Î¸ x ï¼ Î¸ (ğ‘ŸÂ² y))

Î·-is-decidable : (x y : ğ“¢) â†’ is-decidable (Î· x ï¼ Î· y)
Î¸-is-decidable : (x y : ğ“¡) â†’ is-decidable (Î¸ x ï¼ Î¸ y)

fibers-of-Î·-over-ğ‘  ğ¸ y = inr (ğŸ˜-elim âˆ˜ Î·-is-not-Î¸ ğ‘† y âˆ˜ ap s)
fibers-of-Î·-over-ğ‘  ğ‘† y = inr (ğŸ˜-elim âˆ˜ Î·-is-not-Î¸ ğ¸ y âˆ˜ ap s)
fibers-of-Î·-over-ğ‘  (ğ‘  x) y =
  +functor (ap s) (contrapositive s-left-cancellable) (Î¸-is-decidable x y)

fibers-of-Î¸-over-ğ‘Ÿ (ğ‘Ÿ x) y =
  +functor (ap r) (contrapositive r-left-cancellable) (Î·-is-decidable x y)
fibers-of-Î¸-over-ğ‘Ÿ (ğ‘ŸÂ² x) y = inr (ğŸ˜-elim âˆ˜ Î·-is-not-Î¸ x (ğ‘ŸÂ² y) âˆ˜ ap r)

fibers-of-Î¸-over-ğ‘ŸÂ² (ğ‘Ÿ x) y = inr (ğŸ˜-elim âˆ˜ Î·-is-not-Î¸ x (ğ‘Ÿ y) âˆ˜ ap rÂ²)
fibers-of-Î¸-over-ğ‘ŸÂ² (ğ‘ŸÂ² x) y =
  +functor (ap rÂ²) (contrapositive rÂ²-left-cancellable) (Î·-is-decidable x y)

Î·-is-decidable x ğ¸ = fibers-of-Î·-over-E x
Î·-is-decidable x ğ‘† = fibers-of-Î·-over-S x
Î·-is-decidable x (ğ‘  y) = fibers-of-Î·-over-ğ‘  x y

Î¸-is-decidable x (ğ‘Ÿ y) = fibers-of-Î¸-over-ğ‘Ÿ x y
Î¸-is-decidable x (ğ‘ŸÂ² y) = fibers-of-Î¸-over-ğ‘ŸÂ² x y

ğ“œ-is-discrete : is-discrete ğ“œ
ğ“œ-is-discrete (Î· x) (Î· y) = Î·-is-decidable x y
ğ“œ-is-discrete (Î· x) (Î¸ y) = inr (Î·-is-not-Î¸ x y)
ğ“œ-is-discrete (Î¸ x) (Î· y) = inr (â‰ -sym (Î·-is-not-Î¸ y x))
ğ“œ-is-discrete (Î¸ x) (Î¸ y) = Î¸-is-decidable x y

ğ“œ-is-set : is-set ğ“œ
ğ“œ-is-set = discrete-types-are-sets ğ“œ-is-discrete

\end{code}
